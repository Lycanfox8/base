defvarp showserverincompat 0 0 1

ui_servers_opened = 0

uimenu "serversset" "Server Browser Settings" $questiontex [
    uivlist $ui_padbutton [
        uihlist $ui_padbutton [
            uicheckbox "Search LAN for Servers" (!= $searchlan 0) $ui_checksize [searchlan (! $searchlan)] $colourred 0 $ui_default
            uialign -1 0
        ]
        uihlist $ui_padbutton [
            uicheckbox "Automatically Sort Servers" (!= $autosortservers 0) $ui_checksize [autosortservers (! $autosortservers)] $colourred 0 $ui_default
            uialign -1 0
        ]
        uihlist $ui_padbutton [
            uicheckbox "Show Incompatible Servers" (!= $showserverincompat 0) $ui_checksize [showserverincompat (! $showserverincompat)] $colourred 0 $ui_default
            uialign -1 0
        ]
        uibuttonw "Done" [uiclose "serversset"] 0 $colourgreen
    ]
] [] [] [] 1 []

uimenu "servers" "Server Browser" "<grey>textures/servers/list" [
    if (> (- (getmillis 1) $ui_servers_opened) (* $ui_transition 2)) [updateservers]
    uivlist $ui_padbutton [
        uialign -1 -1
        ui_servers_num = (getservercountif ui_servers_cur [|| $showserverincompat (= (getserver $ui_servers_cur 1 0) (getversion 1))])
        ui_servers_numactive = (getserveractiveif ui_servers_cur [|| $showserverincompat (= (getserver $ui_servers_cur 1 0) (getversion 1))])
        ui_servers_numplayers = (getserverplayersif ui_servers_cur [|| $showserverincompat (= (getserver $ui_servers_cur 1 0) (getversion 1))])
        uihlist $ui_padbutton [
            uiclamp 1 1 1 1
            uialign -1 0
            uihlist $ui_padbutton [
                uialign -1 0
                uibuttonz "Update List" [updatefrommaster] 0 $colourgreen [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Clear List" [clearservers] 0 $colourred [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Disconnect" [disconnect] (= (isconnected) 0) $colourorange [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "LAN Connect" [lanconnect] 0 $colouryellow [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Sort Now" [sortservers] 0 $colourviolet [] $ui_buttonzh $ui_text $ui_padsmall
                uibuttonz "Settings" [uiopen "serversset" 0] 0 $colourcyan [] $ui_buttonzh $ui_text $ui_padsmall
            ]
            uihlist $ui_padbutton [
                uialign 1 0
                uitextright (concatword "^fs^fc" $ui_servers_numplayers "^fS player"  (? (!= $ui_servers_numplayers 1) "s") " on ^fs^fc" $ui_servers_numactive "^fS of ^fs^fc" $ui_servers_num "^fS server" (? (!= $ui_servers_num 1) "s"))
            ]
        ]
        uispace $ui_padtiny
        uihlist 0 [
            uiclamp 1 1 1 1
            uialign -1 -1
            uihlist $ui_padsmall [
                uiclamp 1 1 1 1
                uialign -1 -1
                uiscroll 1.5 0.5 [
                    uialign -1 -1
                    uivlist 0 [
                        uihlist 0 [uispace 0.5]
                        uialign -1 -1
                        if (= $ui_servers_num 0) [
                            uispace 0 $ui_padwin
                            uitext "Looking for servers..."
                            uitext "Servers may take several seconds to appear."
                        ]
                        loopserversif ui_servers_cur [|| $showserverincompat (= (getserver $ui_servers_cur 1 0) (getversion 1))] [
                            ui_servers_stat = (getserver $ui_servers_cur 0 0)
                            ui_servers_name = (getserver $ui_servers_cur 0 1)
                            ui_servers_port = (getserver $ui_servers_cur 0 2)
                            ui_servers_npid = [@[ui_servers_name]:[@@ui_servers_port]]
                            ui_servers_lpid = [@[ui_servers_name]_@[ui_servers_port]]
                            ui_servers_desc = (getserver $ui_servers_cur 0 3)
                            ui_servers_mapn = (getserver $ui_servers_cur 0 4)
                            ui_servers_nump = (getserver $ui_servers_cur 0 5)
                            ui_servers_ping = (getserver $ui_servers_cur 0 6)
                            ui_servers_last = (getserver $ui_servers_cur 0 7)
                            ui_servers_handle = (getserver $ui_servers_cur 0 8)
                            ui_servers_flags = (getserver $ui_servers_cur 0 9)
                            ui_servers_bran = (getserver $ui_servers_cur 0 10)
                            ui_servers_prio = (getserver $ui_servers_cur 0 11)
                            ui_servers_attr = (getserver $ui_servers_cur 1 -1)
                            ui_servers_gver = (getserver $ui_servers_cur 1 0)
                            ui_servers_mode = (getserver $ui_servers_cur 1 1)
                            ui_servers_muts = (getserver $ui_servers_cur 1 2)
                            ui_servers_time = (getserver $ui_servers_cur 1 3)
                            ui_servers_maxp = (getserver $ui_servers_cur 1 4)
                            ui_servers_mstr = (getserver $ui_servers_cur 1 5)
                            ui_servers_vars = (getserver $ui_servers_cur 1 6)
                            ui_servers_mods = (getserver $ui_servers_cur 1 7)
                            ui_servers_verm = (getserver $ui_servers_cur 1 8)
                            ui_servers_vern = (getserver $ui_servers_cur 1 9)
                            ui_servers_verp = (getserver $ui_servers_cur 1 10)
                            ui_servers_vers = (getserver $ui_servers_cur 1 11)
                            ui_servers_vera = (getserver $ui_servers_cur 1 12)
                            ui_servers_gmst = (getserver $ui_servers_cur 1 13)
                            ui_servers_gmtl = (getserver $ui_servers_cur 1 14)
                            ui_servers_players = (getserver $ui_servers_cur 2)
                            ui_servers_maptitle = (getmaptitle $ui_servers_mapn)
                            ui_servers_offt = (? (>= $ui_servers_last 0) (div (max (- (getmillis 1) (- $ui_servers_last (div $ui_servers_ping 2))) 0) 1000) 0)
                            ui_servers_active = (? (< $ui_servers_ping $serverwaiting) 1 0)
                            ui_servers_statsflag = (hasflag $ui_servers_flags "s")
                            [ui_tip_servers_@ui_servers_lpid] = [
                                if (> @ui_servers_players 0) [
                                    uitext (concat "Current players on^fa" @@ui_servers_npid) $ui_texttip
                                    ui_servers_pname = ""
                                    loop j @@ui_servers_players [
                                        append ui_servers_pname (format ["%1"] (getserver @@@ui_servers_cur 2 $j) 1)
                                    ]
                                    uitext (prettylist $ui_servers_pname) $ui_texttipsmall [uitextwrap $ui_tipwrap]
                                ] [uitext (concat "No players on^fa" @@ui_servers_npid) $ui_texttip]
                            ]
                            ui_servers_img = $emblemtex
                            if (!=s $ui_servers_mapn "") [
                                if (=s $ui_servers_mapn "<random>") [ui_servers_img = $questiontex] [ui_servers_img = (getmapfile $ui_servers_mapn)]
                            ]
                            uiskinborder 0 0 $ui_main1 $ui_main2 $ui_main3 $ui_line $colourred $colourpink [
                                uirelease [sleep 1 [connect @ui_servers_name @ui_servers_port]]
                                uitooltip [servers_@ui_servers_lpid]
                                uialign -1 0
                                uiclamp 1 1 1 1
                                uispace $ui_padbutton $ui_padsmall [
                                    uialign -1 0
                                    uihlist 0 [
                                        uialign -1 0
                                        uispace $ui_padsmall $ui_padsmall [
                                            uivlist 0 [
                                                uispace 0.035
                                                if $ui_servers_active [
                                                    uihlist 0 [
                                                        uitext (concatword (? $ui_servers_nump "^fw" "^fA") $ui_servers_nump) $ui_textlarge
                                                        uitext (concatword " ^fA/ ^fa" $ui_servers_maxp) $ui_text
                                                    ]
                                                    uitext (concatword (? (< $ui_servers_ping 200) "^fg" (? (< $ui_servers_ping 400) "^fy" "^fr")) $ui_servers_ping " ^fams") $ui_text
                                                    ui_servers_secs = (? (>= $ui_servers_last 0) (div (- (getmillis 1) $ui_servers_last) 1000) -1)
                                                    uitext (? (>= $ui_servers_secs 0) (concatword "^fa" $ui_servers_secs " ^fAs ago") "^fAWaiting..") $ui_textsmall
                                                ] [
                                                    uiimage "<grey>textures/servers/failed" $ui_default 0 $ui_imgbut $ui_imgbut
                                                ]
                                            ]
                                        ]
                                        uispace $ui_padsmall $ui_padsmall [
                                            uiimage $ui_servers_img $ui_default 0 $ui_imglist $ui_imglist [uiimagealttex "textures/nothumb"; uioutline $ui_line; uiclamp- 1 1 1 1]
                                        ]
                                        uispace $ui_padsmall $ui_padtiny [
                                            uialign -1 0
                                            uivlist 0 [
                                                uialign -1 0
                                                uihlist 0 [
                                                    uialign -1 0
                                                    uitextleft $ui_servers_desc (? (> (strlen $ui_servers_desc) 48) $ui_textsmall $ui_text)
                                                    uitextleft (concatword " ^fA(^fa" $ui_servers_npid "^fA)") $ui_textsmall
                                                    //if (!=s $ui_servers_handle "") [uitextleft (concatword " ^fA[^fa" $ui_servers_handle "^fA]") $ui_textsmall]
                                                    if $ui_servers_statsflag [uitextleft " ^fA[^fastatistics^fA]" $ui_textsmall]
                                                    if (>= $ui_servers_attr 13) [uitextleft (concatword " ^fA[^fa" $ui_servers_verm "." $ui_servers_vern "." $ui_servers_verp "-" (platname $ui_servers_vers) $ui_servers_vera "-" $ui_servers_bran "^fA]") $ui_textsmall]
                                                    //uitextleft (concatword " ^fA[^fapriority " $ui_servers_prio "^fA]") $ui_textsmall
                                                ]
                                                uispace 0 $ui_padtiny
                                                uihlist 0 [
                                                    uialign -1 0
                                                    if $ui_servers_active [
                                                        if (= $ui_servers_gver (getversion 1)) [
                                                            uibackskin $ui_main1 $ui_line [
                                                                case $ui_servers_stat 0 [
                                                                    uiimage "<grey>textures/servers/connect" $ui_default 0 $ui_iconsize $ui_iconsize
                                                                    uispace $ui_padtiny
                                                                    uitextleft "^fgOpen" $ui_textsmall
                                                                ] 1 [
                                                                    uiimage "<grey>textures/servers/locked" $ui_default 0 $ui_iconsize $ui_iconsize
                                                                    uispace $ui_padtiny
                                                                    uitextleft "^fyLocked" $ui_textsmall
                                                                ] 2 [
                                                                    uiimage "<grey>textures/servers/failed" $ui_default 0 $ui_iconsize $ui_iconsize
                                                                    uispace $ui_padtiny
                                                                    uitextleft "^fmPrivate" $ui_textsmall
                                                                ] 3 [
                                                                    uiimage "<grey>textures/servers/failed" $ui_default 0 $ui_iconsize $ui_iconsize
                                                                    uispace $ui_padtiny
                                                                    uitextleft "^frFull" $ui_textsmall
                                                                ] () [
                                                                    uiimage "<grey>textures/servers/unknown" $ui_default 0 $ui_iconsize $ui_iconsize
                                                                    uispace $ui_padtiny
                                                                    uitextleft "^foUnknown" $ui_textsmall
                                                                ]
                                                            ]
                                                            if (!= $ui_servers_mode $modeidxediting) [
                                                                uitextleft (concatword " ^fA[^fw" (getgamestatestr 1 $ui_servers_gmst) "^fA: ^fw" (timestr (* (? $ui_servers_nump (max (- $ui_servers_gmtl $ui_servers_offt) 0) $ui_servers_time) 1000) 3) "^fA]") $ui_textsmall
                                                            ]
                                                            ui_servers_gname = (gamename $ui_servers_mode $ui_servers_muts 0 32)
                                                            uitextleft (concatword " ^fy" $ui_servers_gname " ^faon ^fo" $ui_servers_maptitle) $ui_textsmall
                                                            uitextleft (concatword " ^fA(^fa" (? $ui_servers_mods (concatword (precf (*f (divf $ui_servers_mods $ui_servers_vars) 100) 2) "% ") "un") "modified^fA)") $ui_textsmall
                                                        ] [
                                                            uibackskin $ui_main1 $ui_line [
                                                                uiimage "<grey>textures/servers/failed" $ui_default 0 $ui_iconsize $ui_iconsize
                                                                uispace $ui_padtiny
                                                                uitextleft "^foIncompatible" $ui_textsmall
                                                            ]
                                                            uitextleft (concat " ^faServer is using" (? (> $ui_servers_gver (getversion 1)) "a ^fwnewer" "an ^fAolder") "protocol") $ui_textsmall
                                                        ]
                                                    ] [
                                                        uibackskin $ui_main1 $ui_line [
                                                            uiimage "<grey>textures/servers/failed" $ui_default 0 $ui_iconsize $ui_iconsize
                                                            uispace $ui_padtiny
                                                            uitextleft "^foUnresponsive" $ui_textsmall
                                                        ]
                                                        uitextleft " ^faServer is not replying to queries" $ui_textsmall
                                                    ]
                                                ]
                                                uispace 0 $ui_padtiny
                                                uihlist 0 [
                                                    uialign -1 0
                                                    if (> $ui_servers_players 0) [
                                                        uitextleft "^fwWith: " $ui_text
                                                        ui_servers_pname = ""
                                                        ui_servers_pmore = 0
                                                        loop j $ui_servers_players [
                                                            if (|| $ui_servers_pmore (> (strlen (stripcolors $ui_servers_pname)) 128)) [ui_servers_pmore = (+ $ui_servers_pmore 1)] [
                                                                append ui_servers_pname (format ["%1"] (getserver $ui_servers_cur 2 $j) 1)
                                                            ]
                                                        ]
                                                        uitext (concat (prettylist $ui_servers_pname) (? $ui_servers_pmore (concat "and^fy" $ui_servers_pmore "^fwmore"))) $ui_textsmall [uitextwrap 0.75; uitextalign -1; uialign -1 0]
                                                    ] [uitextleft "^faNo current players."]
                                                ]
                                            ]
                                        ]
                                    ]
                                ]
                            ] [uiclamp 1 1 1 1] 1
                        ] [
                            uialign -1 0
                            uiclamp 1 1 1 1
                            uispace $ui_padsmall $ui_padsmall [
                                uialign 0 0
                                uitextleft "Waiting for server list from master server."
                            ]
                        ]
                    ]
                ]
                uivscroll $ui_slidersize 0.5
                uialign- 1 -1
            ]
        ]
    ]
] [
    ui_servers_opened = (getmillis 1)
]

ui_guidelines_text = ""

uimenu "guidelines" "Multiplayer Guidelines" "<grey>textures/icons/waiting" [
    uivlist $ui_padbutton [
        uitext "^fyYou must agree to these guidelines before playing online." $ui_text
        uihlist $ui_padbutton [
            uiscroll 1 0.6 [
                uivlist 0.0008 [
                    uitext $ui_guidelines_text $ui_textsmall [uitextwrap 1; uitextalign -1]
                ]
            ]
            uivscroll $ui_slidersize 0.6
        ]
        uihlist $ui_padbutton [
            uialign 0 1
            uibuttonw "I Agree" [
                connectguidelines 1
                uiclose "guidelines"
                if (!=s $guidelinesaction "") [
                    sleep 1 [
                        @guidelinesaction
                        guidelinesaction ""
                    ]
                ]
            ] 0 $colourgreen
            uibuttonw "I Disagree" [uiclose "guidelines"] 0 $colourred
        ]
    ]
] [
    if $connectguidelines [uiclose "guidelines"] [loadalias ui_guidelines_text "doc/guidelines.txt"]
]
